set shell := ["bash", "-uc"]
ref_name := shell("git rev-parse --abbrev-ref HEAD")
name := shell("grep -Po '(?<=name: ).*$' model/*.linkml.yml | tr -d '\n' | tr '-' '_'")

_default:
    @just --list --unsorted --justfile {{justfile()}}

# Generate documentation
[group("generators")]
generate-documentation: (clean "output")
    @echo "Generating documentation…"
    @echo
    @echo "Generating schema documentation…"
    @echo
    mkdir -p "output"
    cp -r "documentation" "output/adoc"
    mkdir -p "output/adoc/modules/schema"
    python -m linkml_asciidoc_generator.main \
        "model/{{name}}.linkml.yml" \
        "output/adoc/modules/schema" \
        "--relations-diagrams"
    echo -e "nav:\n- modules/schema/nav.adoc" >> output/adoc/antora.yml
    @echo "… OK."
    @echo
    @echo -e "Copying artifacts to documentation…"
    @for model in model/*; do \
        cp -r $model output/adoc/modules/schema/attachments/; \
        echo -e "To reference use:\n\txref:schema:attachment$"${model#model/}; \
    done
    @echo
    @echo "Generating site using Antora…"
    @if [ ${IN_DEVCONTAINER} == "true"  ]; then \
        jinja -D site_url http://localhost:8080 -D site_title 'Netbewust Laden' /antora/local-antora-playbook.yml.jinja > ~/local-antora-playbook.yml; \
        antora ~/local-antora-playbook.yml; \
    else \
        jinja -D site_url http://modellen.netbeheernederland.nl -D site_title 'Netbewust Laden' /antora/antora-playbook.yml.jinja > ~/antora-playbook.yml; \
        antora ~/antora-playbook.yml; \
    fi
    @echo
    @echo -e "Generated documentation files at: output/html"
    @echo

# Clean up the output directory
[group("project")]
clean path:
    @echo "Cleaning up…"
    @echo
    @if [ -d {{path}} ]; then \
        rm -rf {{path}}; \
    fi
    @echo "… OK."
    @echo

# Generate example data
[group("generators")]
generate-example-data:
    @echo "Generating JSON example data…"
    @echo
    mkdir -p "output/examples"
    for example_file in examples/*.yml; do \
        [ -f "$example_file" ] || continue; \
        gen-linkml-profile  \
            convert \
            "$example_file" \
            --out "output/${example_file%.*}.json"; \
    done
    @echo "… OK."
    @echo
    @echo -e "Generated example JSON data at: output/examples"
    @echo

# Generate JSON Schema
[group("generators")]
generate-json-schema: (clean "model/{{name}}.json_schema.json")
    @echo "Generating JSON Schema…"
    @echo
    gen-json-schema \
        --not-closed \
        "model/{{name}}.linkml.yml" \
        > "model/{{name}}.json_schema.json"
    @echo "… OK."
    @echo
    @echo "Generated JSON Schema at: model/{{name}}.json_schema.json"
    @echo

# Validate example data
[group("validate")]
validate-example-data: generate-json-schema generate-example-data
    @echo "Validating example data against JSON schema…"
    @echo
    for example_file in output/examples/*.json; do \
        [ -f "$example_file" ] || continue; \
        check-jsonschema --schemafile "model/{{name}}.json_schema.json" $example_file; \
    done
    @echo "… OK."
    @echo

[group("project")]
serve:
    http-serve output/html

# Preview version
[group("version-control")]
preview-version:
    @echo "Generating preview of version…"
    @echo
    gh workflow run preview_release.yml --ref {{ref_name}}
    @echo "… OK."
    @echo

# Release new major version
[group("version-control")]
release-major-version:
    @echo "Releasing new major version…"
    @echo
    just build
    git add && git commit
    git tag v1.0
    @echo "… OK."
    @echo

# Release new minor version
[group("version-control")]
release-minor-version:
    @echo "Releasing new minor version…"
    @echo
    gh workflow run release_minor_version.yml --ref {{ref_name}}
    @echo "… OK."
    @echo