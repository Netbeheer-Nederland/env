set shell := ["bash", "-uc"]

ref_name := "git rev-parse --abbrev-ref HEAD"
major_branch_name := "git rev-parse --abbrev-ref HEAD | cut -d . -f 1"
name := "grep -Po '(?<=name: ).*$' information_models/*.linkml.yml | tr -d '\n' | tr '-' '_'"


_default:
    @just --list --unsorted --justfile {{justfile()}}

# Build the project
[group("project")]
build: clean _post-process-linkml-schema generate-json-schema generate-documentation generate-example-data validate-example-data
    @echo "Building project…"
    @echo
    cp -r "output/artifacts/information_models" "output/artifacts/documentation/modules/schema/attachments/"
    cp -r "output/artifacts/schemas" "output/artifacts/documentation/modules/schema/attachments/"
    cp -r "output/artifacts/examples" "output/artifacts/documentation/modules/schema/attachments/"
    @echo "… OK."
    @echo
    @echo "All project artifacts have been generated and post-processed, and can found in: output/artifacts/"
    @echo

# Clean up the output directory
[group("project")]
clean:
    @echo "Cleaning up generated artifacts…"
    @echo
    @if [ -d "output/artifacts" ]; then \
        rm -rf "output/artifacts"; \
    fi
    mkdir -p "output/artifacts"
    @echo "… OK."
    @echo

# Post-process LinkML schema for preview or releasing
_post-process-linkml-schema:
    @echo "Copying source files to artifacts directory…"
    @echo
    mkdir -p "output/artifacts/information_models"
    cp "information_models/{{shell(name)}}.linkml.yml" "output/artifacts/information_models/"
    @echo
    @echo "Setting version in LinkML schema…"
    @echo
    sed -i '/^version: .*$/d' "output/artifacts/information_models/{{shell(name)}}.linkml.yml"
    @if [ -z ${VERSION:-} ]; then \
        sed -i "/^name: .*$/a version: {{shell(ref_name)}}" "output/artifacts/information_models/{{shell(name)}}.linkml.yml"; \
    else \
        sed -i "/^name: .*$/a version: ${VERSION}" "output/artifacts/information_models/{{shell(name)}}.linkml.yml"; \
    fi
    @echo "… OK."
    @echo

# Create new draft
[group("version-control")]
create-draft name:
    @echo "Creating new draft…"
    @echo
    @echo "Creating and tracking new draft branch…"
    @echo
    git checkout -b {{shell(major_branch_name) + "." + name}}
    git commit --allow-empty -m "Start working on draft"
    git push -u origin {{shell(major_branch_name) + "." + name}}
    @echo
    @echo "Creating new draft pull request…"
    @echo
    gh pr create --base {{shell(major_branch_name)}} --draft
    @echo "… OK."
    @echo

# Finish draft
[group("version-control")]
finish-draft:
    @echo "Finishing draft…"
    @echo
    @echo "Marking draft pull request as ready for review…"
    @echo
    gh pr ready
    @echo "… OK."
    @echo

# Preview version
[group("version-control")]
preview-version:
    @echo "Generating preview of version…"
    @echo
    gh workflow run preview_release.yml --ref {{shell(ref_name)}}
    @echo "… OK."
    @echo

# Check if currently checked out branch is a major version branch
_on-major-branch:
    @echo "Checking if currently checked out branch is a major version branch…"
    @echo
    @if [ ! {{shell(ref_name)}} == {{shell(major_branch_name)}}  ]; then \
        echo "Releases can only be done from major branches. Please check out the major branch you wish to release from."; \
        exit 1; \
    fi

# Release new major version
[group("version-control")]
release-major-version: _on-major-branch
    @echo "Releasing new major version…"
    @echo
    gh workflow run release_major_version.yml --ref {{shell(ref_name)}}
    @echo "… OK."
    @echo

# Release new minor version
[group("version-control")]
release-minor-version: _on-major-branch
    @echo "Releasing new minor version…"
    @echo
    gh workflow run release_minor_version.yml --ref {{shell(ref_name)}}
    @echo "… OK."
    @echo

# Release new patch version
[group("version-control")]
release-patch-version: _on-major-branch
    @echo "Releasing new patch version…"
    @echo
    gh workflow run release_patch_version.yml --ref {{shell(ref_name)}}
    @echo "… OK."
    @echo

# Generate documentation
[group("generators")]
generate-documentation: _generate_schema_documentation
    cp -r root_documentation output/artifacts/
    @if [ ${IN_DEVCONTAINER} == "true"  ]; then \
        jinja -D site_url http://www.google.com -D site_title 'Netbewust Laden' /antora/local-antora-playbook.yml.jinja > ~/local-antora-playbook.yml; \
        antora ~/local-antora-playbook.yml; \
    else \
        jinja -D site_url http://www.yahoo.com -D site_title 'Netbewust Laden' /antora/antora-playbook.yml.jinja > ~/antora-playbook.yml; \
        antora ~/antora-playbook.yml; \
    fi


# Generate documentation
[group("generators")]
_generate_schema_documentation: _post-process-linkml-schema
    @echo "Generating schema documentation…"
    @echo
    cp -r "documentation" "output/artifacts"
    mkdir -p "output/artifacts/documentation/modules/schema"
    python -m linkml_asciidoc_generator.main \
        "output/artifacts/information_models/{{shell(name)}}.linkml.yml" \
        "output/artifacts/documentation/modules/schema" \
        "--relations-diagrams"
    echo -e "nav:\n- modules/schema/nav.adoc" >> output/artifacts/documentation/antora.yml
    @echo "… OK."
    @echo
    @echo -e "Generated documentation files at: output/artifacts/documentation"
    @echo

# Generate example data
[group("generators")]
generate-example-data: _post-process-linkml-schema
    @echo "Generating JSON example data…"
    @echo
    mkdir -p "output/artifacts/examples"
    for example_file in examples/*.yml; do \
        [ -f "$example_file" ] || continue; \
        gen-linkml-profile  \
            convert \
            "$example_file" \
            --out "output/artifacts/${example_file%.*}.json"; \
    done
    @echo "… OK."
    @echo
    @echo -e "Generated example JSON data at: output/artifacts/examples"
    @echo

# Generate JSON Schema
[group("generators")]
generate-json-schema: _post-process-linkml-schema
    @echo "Generating JSON Schema…"
    @echo
    mkdir -p "output/artifacts/schemas/json_schema"
    gen-json-schema \
        --not-closed \
        "output/artifacts/information_models/{{shell(name)}}.linkml.yml" \
        > "output/artifacts/schemas/json_schema/{{shell(name)}}.json_schema.json"
    @echo "… OK."
    @echo
    @echo "Generated JSON Schema at: output/artifacts/schemas/json_schema/{{shell(name)}}.json_schema.json"
    @echo

# Validate example data
[group("validate")]
validate-example-data: generate-json-schema generate-example-data
    @echo "Validating example data against JSON schema…"
    @echo
    for example_file in output/artifacts/examples/*.json; do \
        [ -f "$example_file" ] || continue; \
        check-jsonschema --schemafile "output/artifacts/schemas/json_schema/{{shell(name)}}.json_schema.json" $example_file; \
    done
    @echo "… OK."
    @echo

serve:
    http-serve output/docs
